apply plugin: "jacoco"

jacoco {
    toolVersion "0.8.8"
    reportsDirectory.set(layout.buildDirectory.dir('reports'))
}

task jacocoTestReport(type: JacocoReport) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"

    reports {
        xml.required.set(true)
        html.required.set(true)
    }

    def excludes = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/*Test*.*',
            '**/*JsonAdapter.*'
    ]

    classDirectories.setFrom(fileTree(
            dir: "$buildDir/intermediates/classes/debug",
            excludes: excludes
    ) + fileTree(
            dir: "$buildDir/tmp/kotlin-classes/debug",
            excludes: excludes
    ))

    sourceDirectories.setFrom(files([
            "src/main/java"
    ]))
    executionData.setFrom(fileTree(
            dir: project.projectDir,
            includes: ["**/*.exec", "**/*.ec"]
    ))
}
