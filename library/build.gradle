import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id('com.google.devtools.ksp').version('2.2.0-2.0.2')
    alias(libs.plugins.dokka)
    alias(libs.plugins.validator)
    alias(libs.plugins.ktlint)
}

apply from: 'jacoco.gradle'

tasks.named("dokkaHtml") {
    moduleName.set("PushPushGo SDK")
}

android {
    namespace 'com.pushpushgo.sdk'
    compileSdk 36

    defaultConfig {
        minSdk 23
        targetSdk 36

        consumerProguardFiles("ppgo-sdk.pro")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    buildFeatures {
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.fromTarget("17")
            languageVersion = KotlinVersion.fromVersion("2.1")
            apiVersion = KotlinVersion.fromVersion("2.1")
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.preference

    implementation libs.kodein

    implementation libs.coroutines.core
    implementation libs.coroutines.android
    implementation libs.coroutines.guava

    implementation libs.retrofit
    implementation libs.retrofit.moshi
    implementation platform(libs.okhttp.bom)
    implementation 'com.squareup.okhttp3:logging-interceptor'

    implementation libs.moshi.kotlin
    ksp libs.moshi.codegen
    implementation libs.moshi.adapters

    implementation libs.androidx.work.runtime
    implementation libs.androidx.work.gcm

    compileOnly platform(libs.firebase.bom)
    compileOnly 'com.google.firebase:firebase-messaging'

    compileOnly libs.hms.push

    // testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.14.5'
    testImplementation 'org.json:json:20250517'
    testImplementation 'androidx.test.ext:junit-ktx:1.3.0'
    testImplementation 'org.robolectric:robolectric:4.15.1'

    testImplementation platform(libs.firebase.bom)
    testImplementation 'com.google.firebase:firebase-messaging'
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.pushpushgo'
            artifactId = 'sdk'
            version = libs.versions.sdk.get()

            afterEvaluate {
                from components.release
            }
        }
    }
}
