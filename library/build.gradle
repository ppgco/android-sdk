plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
    alias(libs.plugins.dokka)
    alias(libs.plugins.validator)
}

apply from: 'jacoco.gradle'

tasks.named("dokkaHtml") {
    moduleName.set("PushPushGo SDK")
}

android {
    compileSdk 33

    namespace 'com.pushpushgo.sdk'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33

        consumerProguardFiles("ppgo-sdk.pro")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaLanguageVersion.of(17)
        targetCompatibility JavaLanguageVersion.of(17)
    }

    kotlinOptions {
        jvmTarget = JavaLanguageVersion.of(17)
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation libs.androidx.preference

    implementation libs.kodein

    implementation libs.coroutines.core
    implementation libs.coroutines.android
    implementation libs.coroutines.guava

    implementation libs.retrofit
    implementation libs.retrofit.moshi
    implementation platform(libs.okhttp.bom)
    implementation 'com.squareup.okhttp3:logging-interceptor'

    implementation libs.moshi
    kapt libs.moshi.codegen
    implementation libs.moshi.adapters

    implementation libs.androidx.work.runtime
    implementation libs.androidx.work.gcm

    compileOnly platform(libs.firebase.bom)
    compileOnly 'com.google.firebase:firebase-messaging-ktx'

    compileOnly libs.hms.push

    // testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.13.7"
    testImplementation 'org.json:json:20230227'
    testImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    testImplementation 'org.robolectric:robolectric:4.10.3'

    testImplementation platform(libs.firebase.bom)
    testImplementation 'com.google.firebase:firebase-messaging-ktx'
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        release(MavenPublication) {
            artifact androidSourcesJar

            groupId = 'com.pushpushgo'
            artifactId = 'sdk'
            version = libs.versions.sdk.get()

            afterEvaluate {
                from components.release
            }
        }
    }
}
